import topology.algebra.ordered

import for_mathlib.filter
import for_mathlib.topology

import valuation.linear_ordered_comm_group_with_zero

/-!
# The topology on linearly ordered commutative groups with zero

Let Î“â‚€ be a linearly ordered commutative group to which we have adjoined a zero element.
Then Î“â‚€ may naturally be endowed with a topology that turns Î“â‚€ into a topological monoid.
The topology is the following:
A subset U âŠ† Î“â‚€ is open if 0 âˆ‰ U or if there is an invertible Î³â‚€ âˆˆ Î“â‚€ such that {Î³ | Î³ < Î³â‚€} âŠ† U.

-/

local attribute [instance, priority 0] classical.decidable_linear_order

local notation `ð“` x: 70 := nhds x

namespace linear_ordered_comm_group_with_zero
open topological_space filter set linear_ordered_structure
variables (Î“â‚€ : Type*) [linear_ordered_comm_group_with_zero Î“â‚€]

/--The neighbourhoods around Î³ âˆˆ Î“â‚€, used in the definition of the topology on Î“â‚€.
These neighbourhoods are defined as follows:
A set s is a neighbourhood of 0 if there is an invertible Î³â‚€ âˆˆ Î“â‚€ such that {Î³ | Î³ < Î³â‚€} âŠ† s.
If Î³ â‰  0, then every set that contains Î³ is a neighbourhood of Î³. -/
def nhds_fun : Î“â‚€ â†’ filter Î“â‚€ :=
  (Î» x : Î“â‚€, if x = 0 then â¨… (Î³â‚€ : units Î“â‚€), principal {Î³ | Î³ < Î³â‚€} else pure x)

/--The topology on a linearly ordered commutative group with a zero element adjoined.
A subset U is open if 0 âˆ‰ U or if there is an invertible element Î³â‚€ such that {Î³ | Î³ < Î³â‚€} âŠ† U. -/
protected def topological_space : topological_space Î“â‚€ :=
topological_space.mk_of_nhds (nhds_fun Î“â‚€)

local attribute [instance] linear_ordered_comm_group_with_zero.topological_space

/--The neighbourhoods {Î³ | Î³ < Î³â‚€} of 0 form a directed set indexed by the invertible elements Î³â‚€.-/
@[sanity_skip]
lemma directed_lt : directed (â‰¥) (Î» (Î³â‚€ : units Î“â‚€), principal {Î³ : Î“â‚€ | Î³ < â†‘Î³â‚€}) :=
begin
  intros Î³â‚ Î³â‚‚,
  use min Î³â‚ Î³â‚‚,
  split,
  { change  principal {Î³ : Î“â‚€ | Î³ < â†‘(min Î³â‚ Î³â‚‚)} â‰¤ principal {Î³ : Î“â‚€ | Î³ < â†‘Î³â‚},
    rw [principal_mono, coe_min],
    intros x x_in,
    calc x < min â†‘Î³â‚ â†‘Î³â‚‚ : x_in
        ... â‰¤ Î³â‚ : min_le_left _ _ },
  { change  principal {Î³ : Î“â‚€ | Î³ < â†‘(min Î³â‚ Î³â‚‚)} â‰¤ principal {Î³ : Î“â‚€ | Î³ < â†‘Î³â‚‚},
    rw [principal_mono, coe_min],
    intros x x_in,
    calc x < min â†‘Î³â‚ â†‘Î³â‚‚ : x_in
        ... â‰¤ Î³â‚‚ : min_le_right _ _ }
end

-- We need two auxilliary lemmas to show that nhds_fun accurately describes the neighbourhoods
-- coming from the topology (that is defined in terms of nhds_fun).

/--At all points of a linearly ordered commutative group with a zero element adjoined,
the pure filter is smaller than the filter given by nhds_fun.-/
private lemma pure_le_nhds_fun : pure â‰¤ nhds_fun Î“â‚€ :=
Î» x, by { by_cases hx : x = 0; simp [hx, nhds_fun] }

/--For every point Î“â‚€, and every â€œneighbourhoodâ€ s of it (described by nhds_fun), there is a
smaller â€œneighbourhoodâ€ t âŠ† s, such that s is a â€œneighbourhoodâ€œ of all the points in t.-/
private lemma nhds_fun_ok : âˆ€ (x : Î“â‚€) (s âˆˆ nhds_fun Î“â‚€ x),
  (âˆƒ t âˆˆ nhds_fun Î“â‚€ x, t âŠ† s âˆ§ âˆ€ y âˆˆ t, s âˆˆ nhds_fun Î“â‚€ y) :=
begin
  intros x U U_in,
  by_cases hx : x = 0,
  { simp [hx, nhds_fun] at U_in âŠ¢,
    rw [mem_infi (directed_lt Î“â‚€) âŸ¨1âŸ©, mem_Union] at U_in,
    cases U_in with Î³â‚€ h,
    use {Î³ : Î“â‚€ | Î³ < â†‘Î³â‚€},
    rw mem_principal_sets at h,
    split,
    { apply mem_infi_sets Î³â‚€,
      rw mem_principal_sets},
    { refine âŸ¨h, _âŸ©,
      intros y y_in,
      by_cases hy : y = 0 ; simp [hy, h y_in],
      { apply mem_infi_sets Î³â‚€,
        rwa mem_principal_sets } } },
  { simp [hx, nhds_fun] at U_in âŠ¢,
    use {x},
    refine âŸ¨mem_singleton _, singleton_subset_iff.2 U_in, _âŸ©,
    intros y y_in,
    rw mem_singleton_iff at y_in,
    rw y_in,
    simpa [hx] }
end

variables  {Î“â‚€}
/--The neighbourhood filter of an invertible element consists of all sets containing that element.-/
@[simp] lemma nhds_coe (Î³ : units Î“â‚€) : ð“ (Î³ : Î“â‚€) = pure (Î³ : Î“â‚€) :=
calc ð“ (Î³ : Î“â‚€) = nhds_fun Î“â‚€ Î³ : nhds_mk_of_nhds (nhds_fun Î“â‚€) Î³ (pure_le_nhds_fun Î“â‚€) (nhds_fun_ok Î“â‚€)
              ... = pure (Î³ : Î“â‚€) : if_neg (group_with_zero.unit_ne_zero Î³)

/--The neighbourhood filter of a nonzero element consists of all sets containing that element.-/
@[simp] lemma nhds_of_ne_zero (Î³ : Î“â‚€) (h : Î³ â‰  0) :
  ð“ Î³ = pure Î³ :=
nhds_coe (group_with_zero.mkâ‚€ _ h)

/--If Î³ is an invertible element of a linearly ordered group with zero element adjoined,
then {Î³} is a neighbourhood of Î³.-/
lemma singleton_nhds_of_units (Î³ : units Î“â‚€) : ({Î³} : set Î“â‚€) âˆˆ ð“ (Î³ : Î“â‚€) :=
by simp

/--If Î³ is a nonzero element of a linearly ordered group with zero element adjoined,
then {Î³} is a neighbourhood of Î³.-/
lemma singleton_nhds_of_ne_zero (Î³ : Î“â‚€) (h : Î³ â‰  0) : ({Î³} : set Î“â‚€) âˆˆ ð“ (Î³ : Î“â‚€) :=
by simp [h]

/--If U is a neighbourhood of 0 in a linearly ordered group with zero element adjoined,
then there exists an invertible element Î³â‚€ such that {Î³ | Î³ < Î³â‚€} âŠ† U.
-/
lemma nhds_zero_mem (U : set Î“â‚€) : U âˆˆ ð“ (0 : Î“â‚€) â†” âˆƒ Î³â‚€ : units Î“â‚€, {Î³ : Î“â‚€ | Î³ < Î³â‚€} âŠ† U :=
begin
  rw nhds_mk_of_nhds (nhds_fun Î“â‚€) 0 (pure_le_nhds_fun Î“â‚€) (nhds_fun_ok Î“â‚€),
  simp [nhds_fun],
  rw mem_infi (directed_lt Î“â‚€) âŸ¨1âŸ©,
  { split,
    { rintro âŸ¨_, âŸ¨Î³â‚€, rflâŸ©, HâŸ©,
      rw mem_principal_sets at H,
      use [Î³â‚€, H] },
    { rintro âŸ¨Î³â‚€, HâŸ©,
      rw mem_Union,
      use Î³â‚€,
      rwa mem_principal_sets } }
end

/--If Î³ is an invertible element of a linearly ordered group with zero element adjoined,
then {x | x < Î³} is a neighbourhood of 0.-/
lemma nhds_zero_of_units (Î³ : units Î“â‚€) : {x : Î“â‚€ | x < Î³} âˆˆ ð“ (0 : Î“â‚€) :=
by { rw nhds_zero_mem, use Î³ }

/--If Î³ is a nonzero element of a linearly ordered group with zero element adjoined,
then {x | x < Î³} is a neighbourhood of 0.-/
lemma nhds_zero_of_ne_zero (Î³ : Î“â‚€) (h : Î³ â‰  0) : {x : Î“â‚€ | x < Î³} âˆˆ ð“ (0 : Î“â‚€) :=
nhds_zero_of_units (group_with_zero.mkâ‚€ _ h)

variable (Î“â‚€)

/--The topology on a linearly ordered group with zero element adjoined
is compatible with the order structure.-/
lemma ordered_topology : ordered_topology Î“â‚€ :=
{ is_closed_le' :=
  begin
    show is_open {p : Î“â‚€ Ã— Î“â‚€ | Â¬p.fst â‰¤ p.snd},
    simp only [not_le],
    rw is_open_iff_mem_nhds,
    rintros âŸ¨a,bâŸ© hab,
    change b < a at hab,
    have ha : a â‰  0 := ne_zero_of_lt hab,
    rw [nhds_prod_eq, mem_prod_iff],
    by_cases hb : b = 0,
    { subst b,
      use [{a}, singleton_nhds_of_ne_zero _ ha, {x : Î“â‚€ | x < a}, nhds_zero_of_ne_zero _ ha],
      intros p p_in,
      cases mem_prod.1 p_in with h1 h2,
      rw mem_singleton_iff at h1,
      change p.2 < p.1,
      rwa h1 },
    { use [{a}, singleton_nhds_of_ne_zero _ ha, {b}, singleton_nhds_of_ne_zero _ hb],
      intros p p_in,
      cases mem_prod.1 p_in with h1 h2,
      rw mem_singleton_iff at h1 h2,
      change p.2 < p.1,
      rwa [h1, h2] }
  end }

local attribute [instance] ordered_topology

/--The topology on a linearly ordered group with zero element adjoined is Tâ‚‚ (aka Hausdorff).-/
lemma t2_space : t2_space Î“â‚€ := ordered_topology.to_t2_space

local attribute [instance] t2_space

/--The topology on a linearly ordered group with zero element adjoined is Tâ‚ƒ (aka regular).-/
lemma regular_space : regular_space Î“â‚€ :=
begin
  haveI : t1_space Î“â‚€ := t2_space.t1_space,
  split,
  intros s x s_closed x_not_in_s,
  by_cases hx : x = 0,
  { refine âŸ¨s, _, subset.refl _, _âŸ©,
    { subst x,
      rw is_open_iff_mem_nhds,
      intros y hy,
      by_cases hy' : y = 0, { subst y, contradiction },
      simpa [hy'] },
    { rw inf_eq_bot_iff,
      use -s,
      simp only [exists_prop, mem_principal_sets],
      split,
      exact mem_nhds_sets (by rwa is_open_compl_iff) (by rwa mem_compl_iff),
      exact âŸ¨s, subset.refl s, by simpâŸ© } },
  { simp only [inf_eq_bot_iff, exists_prop, mem_principal_sets],
    exact âŸ¨-{x}, is_open_compl_iff.mpr is_closed_singleton, by rwa subset_compl_singleton_iff,
          {x}, singleton_nhds_of_ne_zero x hx, -{x}, by simp [subset.refl]âŸ© }
end

/--The filter basis around the 0 element of a linearly ordered group with zero element adjoined.-/
def zero_filter_basis : filter_basis Î“â‚€ :=
{ sets := range (Î» Î³ : units Î“â‚€, {x : Î“â‚€ | x < Î³}),
  ne_empty := range_ne_empty.mpr âŸ¨1âŸ©,
  directed := begin
    intros s t hs ht,
    rw mem_range at hs ht,
    rcases hs with âŸ¨Î³s, rflâŸ©,
    rcases ht with âŸ¨Î³t, rflâŸ©,
    simp only [exists_prop, mem_range],
    rcases directed_lt Î“â‚€ Î³s Î³t with âŸ¨Î³, hs, htâŸ©,
    change principal {g : Î“â‚€ | g < â†‘Î³} â‰¤ principal {g : Î“â‚€ | g < â†‘Î³t} at ht,
    change principal {g : Î“â‚€ | g < â†‘Î³} â‰¤ principal {g : Î“â‚€ | g < â†‘Î³s} at hs,
    rw [le_principal_iff, mem_principal_sets] at hs ht,
    use [{x : Î“â‚€ | x < Î³}, Î³, rfl, subset_inter_iff.mpr âŸ¨hs, htâŸ©]
  end}

variable {Î“â‚€}

-- TODO: Generalise the following definition into something like filter_basis.pure.

/--The filter basis around nonzero elements of
a linearly ordered group with zero element adjoined.-/
@[sanity_skip]
def ne_zero_filter_basis (x : Î“â‚€) : filter_basis Î“â‚€ :=
{ sets := ({({x} : set Î“â‚€)} : set (set Î“â‚€)),
  ne_empty := by simp,
  directed := by finish }

variable (Î“â‚€)

/--The neighbourhood basis of a linearly ordered group with zero element adjoined.-/
def nhds_basis : nhds_basis Î“â‚€ :=
{ B := Î» x, if h : x = 0 then zero_filter_basis Î“â‚€ else ne_zero_filter_basis x,
  is_nhds := begin
    intro x,
    ext s,
    split_ifs with hx,
    { subst x,
      rw nhds_zero_mem,
      simp [zero_filter_basis, filter_basis.mem_filter, filter_basis.mem_iff],
      split,
      { rintros âŸ¨Î³â‚€, hâŸ©,
        use [{x : Î“â‚€ | x < â†‘Î³â‚€}, Î³â‚€, h] },
      { rintros âŸ¨_, âŸ¨Î³â‚€, rflâŸ©, hâŸ©,
        exact âŸ¨Î³â‚€, hâŸ© } },
    { simp [hx, filter_basis.mem_filter, filter_basis.mem_iff, ne_zero_filter_basis], }
  end }

local attribute [instance] nhds_basis

lemma mem_nhds_basis_zero {U : set Î“â‚€} :
  U âˆˆ nhds_basis.B (0 : Î“â‚€) â†” âˆƒ Î³ : units Î“â‚€, U = {x : Î“â‚€ | x < Î³ } :=
begin
  dsimp [nhds_basis, zero_filter_basis],
  simp only [dif_pos],
  convert iff.rfl,
  simp [eq_comm]
end

lemma mem_nhds_basis_ne_zero {U : set Î“â‚€} {Î³â‚€ : Î“â‚€} (h : Î³â‚€ â‰  0) :
  U âˆˆ nhds_basis.B Î³â‚€ â†” U = {Î³â‚€} :=
begin
  dsimp [nhds_basis],
  simp only [dif_neg h],
  dsimp [filter_basis.has_mem, ne_zero_filter_basis Î³â‚€],
  exact set.mem_singleton_iff
end

variable {Î“â‚€}

-- until the end of this section, all linearly ordered commutative groups will be endowed with
-- the discrete topology
variables (Î± : Type*) [linear_ordered_comm_group Î±]

/--The discrete topology on a linearly ordered commutative group.-/
@[sanity_skip] def discrete_ordered_comm_group : topological_space Î± := âŠ¥
local attribute [instance] discrete_ordered_comm_group

lemma ordered_comm_group_is_discrete : discrete_topology Î± := âŸ¨rflâŸ©
local attribute [instance] ordered_comm_group_is_discrete

lemma comap_coe_nhds (Î³ : units Î“â‚€) : ð“ Î³ = comap coe (ð“ (Î³ : Î“â‚€)) :=
begin
  rw [nhds_discrete, filter.comap_pure (Î» _ _ h, units.ext h) Î³],
  change comap coe (pure (Î³ : Î“â‚€)) = comap coe (ð“ â†‘Î³),
  rw â† nhds_coe Î³,
end

lemma tendsto_zero {Î± : Type*} {F : filter Î±} {f : Î± â†’ Î“â‚€} :
  tendsto f F (ð“ (0 : Î“â‚€)) â†” âˆ€ Î³â‚€ : units Î“â‚€, { x : Î± | f x < Î³â‚€ } âˆˆ F :=
begin
  rw nhds_basis.tendsto_into,
  simp only [mem_nhds_basis_zero, exists_imp_distrib],
  split ; intro h,
  { intro Î³â‚€,
    exact h {Î³ | Î³ < â†‘Î³â‚€} Î³â‚€ rfl },
  { rintros _ Î³â‚€ rfl,
    exact h Î³â‚€ }
end

lemma mem_nhds_zero {s} :
  s âˆˆ ð“ (0 : Î“â‚€) â†” âˆƒ Î³ : units Î“â‚€, { x : Î“â‚€ | x < Î³ } âŠ† s :=
begin
  rw nhds_basis.mem_nhds_iff,
  simp only [exists_prop, mem_nhds_basis_zero],
  split,
  { rintros âŸ¨_, âŸ¨âŸ¨Î³, rflâŸ©, hâŸ©âŸ©,
    exact âŸ¨Î³, hâŸ© },
  { rintros âŸ¨Î³, hâŸ©,
    exact âŸ¨{x : Î“â‚€ | x < Î³}, âŸ¨Î³, rflâŸ©, hâŸ© }
end

lemma mem_nhds_coe {s} {Î³ : Î“â‚€} (h : Î³ â‰  0) :
  s âˆˆ ð“ Î³ â†” Î³ âˆˆ s :=
begin
  rw nhds_basis.mem_nhds_iff,
  simp only [exists_prop, mem_nhds_basis_ne_zero _ h, h],
  split,
  { rintros âŸ¨_, rfl, hâ‚‚âŸ©,
    rwa singleton_subset_iff at hâ‚‚ },
  { intro h,
    use [{Î³}, rfl],
    rwa singleton_subset_iff },
end

lemma tendsto_nonzero {Î± : Type*} {F : filter Î±} {f : Î± â†’ Î“â‚€} {Î³â‚€ : Î“â‚€} (h : Î³â‚€ â‰  0) :
  tendsto f F (ð“ (Î³â‚€ : Î“â‚€)) â†” { x : Î± | f x = Î³â‚€ } âˆˆ F :=
begin
  rw nhds_basis.tendsto_into,
  simp only [mem_nhds_basis_ne_zero _ h, forall_eq],
  convert iff.rfl,
  ext s,
  exact mem_singleton_iff.symm
end

/--A linearly ordered commutative group with zero Î“â‚€ is a topological monoid
if it is endowed with the following topology:
A subset U âŠ† Î“â‚€ is open if 0 âˆ‰ U or if there is an invertible Î³â‚€ âˆˆ Î“â‚€ such that {Î³ | Î³ < Î³â‚€} âŠ† U.
-/
instance : topological_monoid Î“â‚€ :=
âŸ¨begin
  rw continuous_iff_continuous_at,
  rintros âŸ¨x, yâŸ©,
  by_cases hx : x = 0; by_cases hy : y = 0,
  all_goals {
    try {subst x}, try {subst y},
    intros U U_in,
    rw nhds_prod_eq,
    try { simp only [_root_.mul_zero, _root_.zero_mul] at U_in},
    rw mem_nhds_zero at U_in <|> rw [mem_nhds_coe] at U_in,
    rw mem_map,
    rw mem_prod_same_iff <|> rw mem_prod_iff,
    try { cases U_in with Î³ hÎ³ } },
  { cases linear_ordered_structure.exists_square_le Î³ with Î³â‚€ hÎ³â‚€,
    simp only [mem_nhds_zero, exists_prop],
    use [{x : Î“â‚€ | x < â†‘Î³â‚€}, Î³â‚€, subset.refl _, {x : Î“â‚€ | x < â†‘Î³â‚€}, Î³â‚€, subset.refl _],
    rw set.prod_subset_iff,
    intros x x_in y y_in,
    apply hÎ³,
    change x*y < Î³,
    have := mul_lt_mul' x_in y_in,
    refine lt_of_lt_of_le this _,
    exact_mod_cast hÎ³â‚€ },
  { simp only [set.prod_subset_iff, mem_nhds_zero, mem_nhds_coe hy, exists_prop],
    use [{x : Î“â‚€ | x < Î³*yâ»Â¹}, Î³ * (group_with_zero.mkâ‚€ y hy)â»Â¹, subset.refl _,
      {(group_with_zero.mkâ‚€ y hy)}, mem_singleton y],
    intros x x_in y' y'_in,
    rw mem_singleton_iff at y'_in,
    rw y'_in,
    apply hÎ³,
    change x * y < Î³,
    simpa [hy] using mul_lt_right' y x_in hy, },
  { simp only [set.prod_subset_iff, mem_nhds_zero, mem_nhds_coe hx, exists_prop],
    use [{(group_with_zero.mkâ‚€ x hx)}, mem_singleton _, {y : Î“â‚€ | y < Î³*xâ»Â¹},
      Î³ * (group_with_zero.mkâ‚€ x hx)â»Â¹, subset.refl _],
    intros x' x'_in y y_in,
    rw mem_singleton_iff at x'_in,
    rw x'_in,
    apply hÎ³,
    change x * y < Î³,
    rw mul_comm,
    simpa [hx] using mul_lt_right' x y_in hx, },
  { simp [mem_nhds_coe, hx, hy],
    use [{x}, mem_singleton _, {y}, mem_singleton _],
    rw set.prod_subset_iff,
    intros x' x'_in y' y'_in,
    rw mem_singleton_iff at *,
    rw [x'_in, y'_in],
    simpa using U_in },
  { assume H, simp at H, tauto }
endâŸ©

end linear_ordered_comm_group_with_zero
