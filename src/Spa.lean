import ring_theory.localization
import ring_theory.subring
import continuous_valuations
import Huber_pair
import Huber_ring.localization

import for_mathlib.nonarchimedean.basic
import for_mathlib.group -- some stupid lemma about units
import for_mathlib.sheaves.presheaf_of_topological_rings
import for_mathlib.topological_rings -- subring of a top ring

universes u‚ÇÅ u‚ÇÇ u‚ÇÉ

local attribute [instance, priority 0] classical.prop_decidable
local attribute [instance] set.pointwise_mul_comm_semiring
local attribute [instance] set.pointwise_mul_action

open set function Spv valuation

local postfix `‚Å∫` : 66 := Œª A : Huber_pair, A.plus

variables {Œì‚ÇÄ : Type*} [linear_ordered_comm_group_with_zero Œì‚ÇÄ]

-- Wedhorn def 7.23.
definition spa (A : Huber_pair) : set (Spv A) :=
{v | v.is_continuous ‚àß ‚àÄ r : A‚Å∫, v (algebra_map A r) ‚â§ 1}

lemma mk_mem_spa {A : Huber_pair} {v : valuation A Œì‚ÇÄ} :
  Spv.mk v ‚àà spa A ‚Üî v.is_continuous ‚àß ‚àÄ r : A‚Å∫, v (algebra_map A r) ‚â§ 1 :=
begin
  apply and_congr,
  { apply is_equiv.is_continuous_iff,
    apply out_mk, },
  { apply forall_congr,
    intro r,
    convert (out_mk v) (algebra_map A r) 1;
    rw [valuation.map_one] }
end

namespace spa

variable {A : Huber_pair}

instance : has_coe (spa A) (Spv A) := ‚ü®subtype.val‚ü©

definition basic_open (r s : A) : set (spa A) :=
{v | v r ‚â§ v s ‚àß v s ‚â† 0 }

lemma mk_mem_basic_open {r s : A} {v : valuation A Œì‚ÇÄ} {hv : Spv.mk v ‚àà spa A} :
(‚ü®mk v, hv‚ü© : spa A) ‚àà basic_open r s ‚Üî v r ‚â§ v s ‚àß v s ‚â† 0 :=
begin
  apply and_congr,
  { apply out_mk, },
  { apply (out_mk v).ne_zero, },
end

lemma basic_open_eq (s : A) : basic_open s s = {v | v s ‚â† 0} :=
set.ext $ Œª v, ‚ü®Œª h, h.right, Œª h, ‚ü®le_refl _, h‚ü©‚ü©

-- should only be applied with (Hfin : fintype T) and (Hopen: is_open (span T))
definition rational_open (s : A) (T : set A) : set (spa A) :=
{v | (‚àÄ t ‚àà T, (v t ‚â§ v s)) ‚àß (v s ‚â† 0)}

-- Here's everything in one package.
structure rational_open_data (A : Huber_pair) :=
(s : A)
(T : set A)
(Tfin : fintype T)
(Hopen : is_open ((ideal.span T) : set A))

instance (r : rational_open_data A) : fintype r.T := r.Tfin

namespace rational_open_data

def ext (r‚ÇÅ r‚ÇÇ : rational_open_data A) (hs : r‚ÇÅ.s = r‚ÇÇ.s) (hT : r‚ÇÅ.T = r‚ÇÇ.T) :
  r‚ÇÅ = r‚ÇÇ :=
begin
  cases r‚ÇÅ, cases r‚ÇÇ,
  congr; assumption
end

def rational_open (r : rational_open_data A) : set (spa A) :=
rational_open r.s r.T

def localization (r : rational_open_data A) := Huber_ring.away r.T r.s

open algebra
variables (r : rational_open_data A)

instance : comm_ring (localization r) :=
by unfold localization ; apply_instance

instance : subgroups_basis (localization r) :=
Huber_ring.away.top_loc_basis r.T r.s r.Hopen

instance : topological_space (localization r) :=
subgroups_basis.topology _

instance : topological_ring (localization r) :=
ring_filter_basis.is_topological_ring _ rfl

instance : algebra A (localization r) := Huber_ring.away.algebra r.T r.s

instance : has_coe A (localization r) := ‚ü®Œª a, (of_id A (localization r) : A ‚Üí localization r) a‚ü©

/- In this file, we are going to take a projective limit over a preordered set of rings,
   to make a presheaf. The underlying type of this preorder is `rational_open_data A`.

 The correct preorder on rational open data:

def correct_preorder : preorder (rational_open_data A) :=
{ le := Œª r1 r2, rational_open r1 ‚äÜ rational_open r2,
  le_refl := Œª _ _, id,
  le_trans := Œª _ _ _, subset.trans,
}

One can prove (in maths) that r1 ‚â§ r2 iff there's a continuous R-algebra morphism
of Huber pairs localization r2 ‚Üí localization r1. I think the ‚Üê direction of this
iff is straightforward (but I didn't think about it too carefully). However we
definitely cannot prove the ‚Üí direction of this iff in this repo yet because we
don't have enough API for cont. Here is an indication
of part of the problem. localization r2 is just A[1/r2.s]. But we cannot prove yet r2.s is
invertible in localization.r1, even though we know it doesn't canish anywhere on
rational_open r2 and hence on rational_open r1, because the fact that it doesn't vanish anywhere
on rational_open r1 only means that it's not in any prime ideal corresponding
to a *continuous* valuation on localization r1 which is bounded by 1 on some + subring;
one would now need to prove, at least, that every maximal ideal
is the support of a continuous valuation, which is Wedhorn 7.52(2). This is not
too bad -- but it is work that we have not yet done. However this is by no means the whole story;
we would also need that r1.T is power-bounded in localization.r2
and this looks much worse: it's Wedhorn 7.52(1). Everything is do-able, but it's just *long*.
Long as in "thousands more lines of code". We will need a good theory of primary and
secondary specialisation of valuations and so on and so on. None of this is there at
the time of writing, although I see no obstruction to putting it there, other than the
fact that it would take weeks of work.

We have to work with a weaker preorder then, because haven't made a good enough
API for continuous valuations. We basically work with the preorder r1 ‚â§ r2 iff
there's a continuous R-algebra map localization r2 ‚Üí localization r1, i.e, we
define our way around the problem. We are fortunate in that we can prove
(in maths) that the projective limit over this preorder agrees with the projective
limit over the correct preorder.
-/

-- note: I don't think we ever use le_refl or le_trans. I only proved them to
-- validate the paper calculation I did which proves that the limit over these things
-- equals the limit over the things we'd rather be taking a limit over.
instance : preorder (rational_open_data A) :=
{ le := Œª r1 r2, ‚àÉ k : A, r1.s * k = r2.s ‚àß
    ‚àÄ t‚ÇÅ ‚àà r1.T, ‚àÉ t‚ÇÇ ‚àà r2.T, ‚àÉ N : ‚Ñï, r2.s ^ N * t‚ÇÇ = r2.s ^ N * (t‚ÇÅ * k),
  le_refl := Œª r, ‚ü®1, mul_one _, Œª t ht, ‚ü®t, ht, 0, by rw mul_one‚ü©‚ü©,
  le_trans := Œª a b c ‚ü®k, hk, hab‚ü© ‚ü®l, hl, hbc‚ü©, ‚ü®k * l, by rw [‚Üêmul_assoc, hk, hl],
    Œª ta hta, begin
      rcases hab ta hta with ‚ü®tb, htb, Nab, h1‚ü©,
      rcases hbc tb htb with ‚ü®hc, htc, Nbc, h2‚ü©,
      use hc, use htc, use (Nab + Nbc),
      rw [‚Üêmul_assoc, pow_add, mul_assoc, h2, ‚Üêhl, mul_pow, mul_pow],
      rw (show b.s ^ Nab * l ^ Nab * (b.s ^ Nbc * l ^ Nbc * (tb * l)) =
        b.s ^ Nab * tb * (l ^ Nab * (b.s ^ Nbc * l ^ Nbc *  l)), by ring),
      rw h1,
      ring
    end‚ü©
}

-- our preorder is weaker than the preorder we're supposed to have but don't. However
-- the projective limit we take over our preorder is provably (in maths) equal to
-- the projective limit that we cannot even formalise. The thing we definitely need
-- is that if r1 ‚â§ r2 then there's a map localization r1 ‚Üí localization r2

/-- This awful function produces r1.s as a unit in localization r2 -/
noncomputable def s_inv_aux (r1 r2 : rational_open_data A) (h : r1 ‚â§ r2) : units (localization r2) :=
@units.unit_of_mul_left_eq_unit _ _
  ((of_id A (localization r2)).to_fun r1.s)
  ((of_id A (localization r2)).to_fun (classical.some h))
  (localization.to_units (‚ü®r2.s, 1, by simp‚ü© : powers r2.s)) (begin
    suffices : (of_id A (localization r2)).to_fun (r1.s * classical.some h) =
      (localization.to_units (‚ü®r2.s, 1, by simp‚ü© : powers r2.s)).val,
      convert this,
      convert (is_ring_hom.map_mul _).symm,
      apply_instance, -- stupid type class inference
    rw (classical.some_spec h).1,
    refl,
end)

/-- The map A(T1/s1) -> A(T2/s2) coming from the inequality r1 ‚â§ r2 -/
noncomputable def localization_map {r1 r2 : rational_open_data A} (h : r1 ‚â§ r2) :
  localization r1 ‚Üí localization r2 :=
Huber_ring.away.lift r1.T r1.s
( show ((s_inv_aux r1 r2 h)‚Åª¬π).inv = (of_id A (localization r2)).to_fun r1.s, from rfl)

instance {r1 r2 : rational_open_data A} (h : r1 ‚â§ r2) : is_ring_hom
(localization_map h) := by delta localization_map; apply_instance

lemma localization.nonarchimedean (r : rational_open_data A) :
  topological_add_group.nonarchimedean (localization r) :=
subgroups_basis.nonarchimedean

section
open localization submodule Huber_ring.away

def localization.power_bounded_data (r : rational_open_data A) : set (localization r) :=
let s_inv : localization r := ((to_units ‚ü®r.s, ‚ü®1, by simp‚ü©‚ü©)‚Åª¬π : units (localization r)) in
(s_inv ‚Ä¢ of_id A (localization r) '' r.T)

set_option class.instance_max_depth 50

theorem localization.power_bounded (r : rational_open_data A) :
  is_power_bounded_subset (localization.power_bounded_data r) :=
begin
  --haveI := Huber_ring.away.is_basis r.T r.s r.Hopen,
  apply is_bounded.subset,
  work_on_goal 0 { apply add_group.subset_closure },
  show is_bounded (ring.closure (localization.power_bounded_data r)),
  intros U hU,
  rcases subgroups_basis.mem_nhds_zero.mp hU with ‚ü®_, ‚ü®V, rfl‚ü©, hV‚ü©,
  refine ‚ü®_, mem_nhds_sets (subgroups_basis.is_op _ rfl (mem_range_self _)) _, _‚ü©,
  { exact V },
  { erw submodule.mem_coe,
    convert submodule.zero_mem _ },
  { intros v hv b hb,
    apply hV,
    rw mul_comm,
    rw submodule.mem_coe at hv ‚ä¢,
    convert submodule.smul_mem _ _ hv,
    work_on_goal 1 { exact ‚ü®b, hb‚ü© },
    refl }
end

end -- section

-- To prove continuity of the localisation map coming from r1 ‚â§ r2 I need to check
-- that the image of T/s in the r1 ring is power-bounded in the r2 ring. This is
-- this lemma.

lemma localization_map_is_cts_aux {r1 r2 : rational_open_data A} (h : r1 ‚â§ r2) :
is_power_bounded_subset
  ((s_inv_aux r1 r2 h)‚Åª¬π.val ‚Ä¢ (Œª (x : ‚Ü•A), to_fun (localization r2) x) '' r1.T) :=
begin
  refine power_bounded.subset _ (localization.power_bounded r2),
  intros x hx,
  rcases hx with ‚ü®y, hy, hz, ‚ü®t‚ÇÅ, ht‚ÇÅ, rfl‚ü©, rfl‚ü©,
  rw mem_singleton_iff at hy, rw hy, clear hy, clear y,
  let h' := h, -- need it later
  rcases h with ‚ü®a, ha, h‚ÇÇ‚ü©,
  rcases h‚ÇÇ t‚ÇÅ ht‚ÇÅ with ‚ü®t‚ÇÇ, ht‚ÇÇ, N, hN‚ü©,
  show ‚Üë(s_inv_aux r1 r2 _)‚Åª¬π * to_fun (localization r2) t‚ÇÅ ‚àà
    localization.mk 1 ‚ü®r2.s, _‚ü© ‚Ä¢ (of_id ‚Ü•A (localization r2)).to_fun '' r2.T,
  rw mem_smul_set,
  use (of_id ‚Ü•A (localization r2)).to_fun t‚ÇÇ,
  existsi _, swap,
    rw mem_image, use t‚ÇÇ, use ht‚ÇÇ,
  rw [‚Üêunits.mul_left_inj (s_inv_aux r1 r2 h'), units.mul_inv_cancel_left],
  show to_fun (localization r2) t‚ÇÅ = to_fun (localization r2) (r1.s) *
    (localization.mk 1 ‚ü®r2.s, _‚ü© * to_fun (localization r2) t‚ÇÇ),
  rw [mul_comm, mul_assoc],
  rw ‚Üêunits.mul_left_inj (localization.to_units (‚ü®r2.s, 1, by simp‚ü© : powers r2.s)),
  rw ‚Üêmul_assoc,
  -- t1=s1*(1/s2 * t2) in r2
  have : ‚Üë(localization.to_units (‚ü®r2.s, 1, by simp‚ü© : powers r2.s)) *
    localization.mk (1 : A) (‚ü®r2.s, 1, by simp‚ü© : powers r2.s) = 1,
  convert units.mul_inv _,
  rw [this, one_mul], clear this,
  show to_fun (localization r2) r2.s * _ = _,
  rw ‚Üêunits.mul_left_inj (localization.to_units (‚ü®r2.s ^ N, N, rfl‚ü© : powers r2.s)),
  show to_fun (localization r2) (r2.s ^ N) * _ = to_fun (localization r2) (r2.s ^ N) * _,
  have hrh : is_ring_hom (to_fun (localization r2)) := begin
    change is_ring_hom ((of_id ‚Ü•A (localization r2)).to_fun),
    apply_instance,
  end,
  rw ‚Üê@is_ring_hom.map_mul _ _ _ _ (to_fun (localization r2)) hrh,
  rw ‚Üê@is_ring_hom.map_mul _ _ _ _ (to_fun (localization r2)) hrh,
  rw ‚Üê@is_ring_hom.map_mul _ _ _ _ (to_fun (localization r2)) hrh,
  rw ‚Üê@is_ring_hom.map_mul _ _ _ _ (to_fun (localization r2)) hrh,
  congr' 1,
  rw [‚Üêmul_assoc _ t‚ÇÇ, hN],
  rw ‚Üêha, ring,
end

-- Continuity now follows from the universal property.
lemma localization_map_is_cts {r1 r2 : rational_open_data A} (h : r1 ‚â§ r2) :
  continuous (localization_map h) := Huber_ring.away.lift_continuous r1.T r1.s
  (localization.nonarchimedean r2) (Huber_ring.away.of_continuous r2.T r2.s _) _ _
  (localization_map_is_cts_aux h)

noncomputable def insert_s (r : rational_open_data A) : rational_open_data A :=
{ s := r.s,
  T := insert r.s r.T,
  Tfin := set.fintype_insert r.s r.T, -- noncomputable!
  Hopen := submodule.is_open_of_open_submodule
    ‚ü®ideal.span (r.T), r.Hopen, ideal.span_mono $ set.subset_insert _ _‚ü©
}


end rational_open_data -- namespace

lemma mk_mem_rational_open {s : A} {T : set A} {v : valuation A Œì‚ÇÄ} {hv : Spv.mk v ‚àà spa A} :
  (‚ü®mk v, hv‚ü© : spa A) ‚àà rational_open s T ‚Üî (‚àÄ t ‚àà T, (v t ‚â§ v s)) ‚àß (v s ‚â† 0) :=
begin
  apply and_congr,
  { apply forall_congr,
    intro t,
    apply forall_congr,
    intro ht,
    apply out_mk },
  { apply (out_mk v).ne_zero }
end

definition rational_open_bInter (s : A) (T : set A) :
  rational_open s T = (‚ãÇ t ‚àà T, basic_open t s) ‚à© {v | v s ‚â† 0} :=
begin
  ext v,
  split; rintros ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©; split; try { exact h‚ÇÇ },
  { erw set.mem_bInter_iff,
    intros t ht,
    split,
    { exact h‚ÇÅ t ht, },
    { exact h‚ÇÇ } },
  { intros t ht,
    erw set.mem_bInter_iff at h‚ÇÅ,
    exact (h‚ÇÅ t ht).1 }
end

lemma rational_open_add_s (s : A) (T : set A) :
  rational_open s T = rational_open s (insert s T) :=
begin
  ext v,
  split; rintros ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©; split; try { exact h‚ÇÇ }; intros t ht,
  { cases ht,
    { rw ht },
    { exact h‚ÇÅ t ht } },
  { apply h‚ÇÅ t,
    exact mem_insert_of_mem _ ht }
end

namespace rational_open_data

lemma insert_s_rational_open (r : rational_open_data A) :
(insert_s r).rational_open = r.rational_open := (rational_open_add_s r.s r.T).symm

lemma mem_insert_s (r : rational_open_data A) :
r.s ‚àà (insert_s r).T := by {left, refl}

end rational_open_data

instance (A : Huber_pair) : topological_space (spa A) :=
topological_space.generate_from {U : set (spa A) | ‚àÉ r : rational_open_data A, U = r.rational_open}

lemma rational_open_inter.aux‚ÇÅ {s‚ÇÅ s‚ÇÇ : A} {T‚ÇÅ T‚ÇÇ : set A}
  (h‚ÇÅ : s‚ÇÅ ‚àà T‚ÇÅ) (h‚ÇÇ : s‚ÇÇ ‚àà T‚ÇÇ) :
  rational_open s‚ÇÅ T‚ÇÅ ‚à© rational_open s‚ÇÇ T‚ÇÇ ‚äÜ
  rational_open (s‚ÇÅ * s‚ÇÇ) (T‚ÇÅ * T‚ÇÇ) :=
begin
  rintros v ‚ü®‚ü®hv‚ÇÅ, hs‚ÇÅ‚ü©, ‚ü®hv‚ÇÇ, hs‚ÇÇ‚ü©‚ü©,
  split,
  { rintros t ‚ü®t‚ÇÅ, ht‚ÇÅ, t‚ÇÇ, ht‚ÇÇ, rfl‚ü©,
    convert le_trans
      (linear_ordered_structure.mul_le_mul_right (hv‚ÇÅ t‚ÇÅ ht‚ÇÅ) _)
      (linear_ordered_structure.mul_le_mul_left  (hv‚ÇÇ t‚ÇÇ ht‚ÇÇ) _);
    apply valuation.map_mul },
  { assume eq_zero, simp at eq_zero, tauto },
end

lemma rational_open_inter.aux‚ÇÇ {s‚ÇÅ s‚ÇÇ : A} {T‚ÇÅ T‚ÇÇ : set A}
  (h‚ÇÅ : s‚ÇÅ ‚àà T‚ÇÅ) (h‚ÇÇ : s‚ÇÇ ‚àà T‚ÇÇ) :
  rational_open (s‚ÇÅ * s‚ÇÇ) (T‚ÇÅ * T‚ÇÇ) ‚äÜ
  rational_open s‚ÇÅ T‚ÇÅ ‚à© rational_open s‚ÇÇ T‚ÇÇ :=
begin
  rintros v ‚ü®hv, hs‚ü©,
  have vmuls : v (s‚ÇÅ * s‚ÇÇ) = v s‚ÇÅ * v s‚ÇÇ := valuation.map_mul _ _ _,
  have hs‚ÇÅ : v s‚ÇÅ ‚â† 0 := Œª H, by simpa [-coe_fn_coe_base, vmuls, H] using hs,
  have hs‚ÇÇ : v s‚ÇÇ ‚â† 0 := Œª H, by simpa [-coe_fn_coe_base, vmuls, H] using hs,
  split; split; try { assumption };
  intros t ht,
  { suffices H : v t * v s‚ÇÇ ‚â§ v s‚ÇÅ * v s‚ÇÇ,
    { simpa [hs‚ÇÇ, mul_assoc, -coe_fn_coe_base] using
        linear_ordered_structure.mul_le_mul_right H (group_with_zero.mk‚ÇÄ _ hs‚ÇÇ)‚Åª¬π, },
    { simpa using hv (t * s‚ÇÇ) ‚ü®t, ht, s‚ÇÇ, h‚ÇÇ, rfl‚ü©, } },
  { suffices H : v s‚ÇÅ * v t ‚â§ v s‚ÇÅ * v s‚ÇÇ,
    { simpa [hs‚ÇÅ, mul_assoc, -coe_fn_coe_base] using
        linear_ordered_structure.mul_le_mul_left H (group_with_zero.mk‚ÇÄ _ hs‚ÇÅ)‚Åª¬π, },
    { simpa using hv (s‚ÇÅ * t) ‚ü®s‚ÇÅ, h‚ÇÅ, t, ht, rfl‚ü©, } }
end

lemma rational_open_inter {s‚ÇÅ s‚ÇÇ : A} {T‚ÇÅ T‚ÇÇ : set A} (h‚ÇÅ : s‚ÇÅ ‚àà T‚ÇÅ) (h‚ÇÇ : s‚ÇÇ ‚àà T‚ÇÇ) :
  rational_open s‚ÇÅ T‚ÇÅ ‚à© rational_open s‚ÇÇ T‚ÇÇ =
  rational_open (s‚ÇÅ * s‚ÇÇ) (T‚ÇÅ * T‚ÇÇ) :=
le_antisymm (rational_open_inter.aux‚ÇÅ h‚ÇÅ h‚ÇÇ) (rational_open_inter.aux‚ÇÇ h‚ÇÅ h‚ÇÇ)

@[simp] lemma rational_open_singleton {r s : A} :
rational_open s {r} = basic_open r s :=
begin
  apply le_antisymm; rintros v ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©; split;
  intros; simp [*] at *,
end

@[simp] lemma basic_open_eq_univ : basic_open (1 : A) (1 : A) = univ :=
univ_subset_iff.1 $ Œª v h, ‚ü®le_refl _,by erw valuation.map_one; exact one_ne_zero‚ü©

@[simp] lemma rational_open_eq_univ : rational_open (1 : A) {(1 : A)} = univ :=
by simp

def rational_basis (A : Huber_pair) : set (set (spa A)) :=
{U : set (spa A) | ‚àÉ r : rational_open_data A, U = r.rational_open }

section
open algebra lattice

lemma rational_basis.is_basis.mul (T‚ÇÅ T‚ÇÇ : set A)
  (h‚ÇÅ : is_open (‚Üë(ideal.span T‚ÇÅ) : set A)) (h‚ÇÇ : is_open (‚Üë(ideal.span T‚ÇÇ) : set A)) :
  is_open (‚Üë(ideal.span (T‚ÇÅ * T‚ÇÇ)) : set A) :=
begin
  rcases Huber_ring.exists_pod_subset _ (mem_nhds_sets h‚ÇÅ $ ideal.zero_mem $ ideal.span T‚ÇÅ)
    with ‚ü®A‚ÇÄ, _, _, _, ‚ü®_, emb, I, fg, top‚ü©, hI‚ü©,
  dsimp only at hI,
  resetI,
  rw is_ideal_adic_iff at top,
  cases top.2 (algebra_map A ‚Åª¬π' ‚Üë(ideal.span T‚ÇÇ)) _ with n hn,
  { apply submodule.is_open_of_open_submodule,
    use ideal.map (of_id A‚ÇÄ A) (I^(n+1)),
    refine ‚ü®is_open_ideal_map_open_embedding emb _ (top.1 (n+1)), _‚ü©,
    delta ideal.span,
    erw [pow_succ, ideal.map_mul, ‚Üê submodule.span_mul_span],
    apply submodule.mul_le_mul,
    { exact (ideal.span_le.mpr hI) },
    { rw ‚Üê image_subset_iff at hn,
      exact (ideal.span_le.mpr hn) } },
  { apply emb.continuous.tendsto,
    rw show algebra.to_fun A (0:A‚ÇÄ) = 0,
    { apply is_ring_hom.map_zero },
    exact (mem_nhds_sets h‚ÇÇ $ ideal.zero_mem $ ideal.span T‚ÇÇ) }
end

end

namespace rational_open_data

noncomputable def inter_aux (r1 r2 : rational_open_data A) : rational_open_data A :=
{ s := r1.s * r2.s,
  T := r1.T * r2.T,
  Tfin := set.pointwise_mul_fintype _ _,
  Hopen := rational_basis.is_basis.mul r1.T r2.T r1.Hopen r2.Hopen
}

noncomputable def inter (r1 r2 : rational_open_data A) : rational_open_data A :=
inter_aux (rational_open_data.insert_s r1) (rational_open_data.insert_s r2)

lemma rational_open_data_inter (r1 r2 : rational_open_data A) :
(inter r1 r2).rational_open = r1.rational_open ‚à© r2.rational_open :=
begin
  rw ‚Üêinsert_s_rational_open r1,
  rw ‚Üêinsert_s_rational_open r2,
  exact (rational_open_inter (mem_insert_s r1) (mem_insert_s r2)).symm
end

lemma rational_open_data_le_inter_left (r1 r2 : rational_open_data A) :
r1 ‚â§ (inter r1 r2) :=
begin
  use r2.s,
  split, refl,
  intros t1 ht1,
  use t1 * r2.s,
  existsi _,
    use 0,
  use t1,
  existsi _,
    use r2.s,
    existsi _, refl,
    exact mem_insert_s r2,
  right, assumption
end

lemma rational_open_data_le_inter_right (r1 r2 : rational_open_data A) :
r2 ‚â§ (inter r1 r2) :=
begin
  use r1.s,
  split, apply mul_comm,
  intros t2 ht2,
  use t2 * r1.s,
  existsi _,
    use 0,
  use r1.s,
  existsi _,
    use t2,
    existsi _, apply mul_comm,
    right, assumption,
  exact mem_insert_s r1,
end

lemma rational_open_data_symm (r1 r2 : rational_open_data A) :
  inter r1 r2 = inter r2 r1 :=
begin
  cases r1,
  cases r2,
    unfold inter inter_aux,
  congr' 1,
    unfold insert_s,
    dsimp, exact mul_comm _ _,
  unfold insert_s,
  dsimp,
  exact mul_comm _ _,
end

end rational_open_data

lemma rational_basis.is_basis.pow (T : set A) (hT : is_open (‚Üë(ideal.span T) : set A)) (n : ‚Ñï) :
  is_open (‚Üë(ideal.span (T^n)) : set A) :=
begin
  induction n with n ih,
  { erw [pow_zero, ideal.span_singleton_one], exact is_open_univ },
  { rw pow_succ, exact rational_basis.is_basis.mul _ _ hT ih }
end

variable (A)

def rational_open_data.univ : rational_open_data A :=
{ s := 1,
  T := {1},
  Tfin := by apply_instance,
  Hopen :=
  begin
    rw ideal.span_singleton_one,
    exact is_open_univ
  end }

lemma rational_open_data_univ :
  (rational_open_data.univ A).rational_open = univ :=
begin
  apply subset.antisymm (subset_univ _),
  intros v hv,
  split,
  { intros t ht,
    erw mem_singleton_iff at ht,
    subst ht,
    exact le_refl _ },
  { show v 1 ‚â† 0,
    erw Spv.map_one,
    simp }
end

lemma rational_basis.is_basis : topological_space.is_topological_basis (rational_basis A) :=
begin
  refine ‚ü®_, _, rfl‚ü©,
  { rintros _ ‚ü®r‚ÇÅ, rfl‚ü© _ ‚ü®r‚ÇÇ, rfl‚ü© x hx,
    refine ‚ü®_, _, hx, subset.refl _‚ü©,
    { use rational_open_data.inter r‚ÇÅ r‚ÇÇ,
      symmetry,
      apply rational_open_data.rational_open_data_inter } },
  { apply subset.antisymm (subset_univ _),
    apply subset_sUnion_of_mem,
    exact ‚ü®_, (rational_open_data_univ A).symm‚ü© }
end

variable {A}

section
open topological_space

def rational_open_data_subsets (U : opens (spa A)) :=
{ r : rational_open_data A // r.rational_open ‚äÜ U}
def rational_open_data_subsets.map {U V : opens (spa A)} (hUV : U ‚â§ V)
  (rd : rational_open_data_subsets U) :
  rational_open_data_subsets V :=
‚ü®rd.val, set.subset.trans rd.property hUV‚ü©

noncomputable def rational_open_data_subsets_inter {U :  opens (spa A)}
  (r1 r2 : rational_open_data_subsets U) :
rational_open_data_subsets U :=
‚ü®rational_open_data.inter r1.1 r2.1, begin
  rw rational_open_data.rational_open_data_inter,
  refine set.subset.trans (inter_subset_left r1.1.rational_open r2.1.rational_open) _,
  exact r1.2
end‚ü©

lemma rational_open_data_subsets_symm {U :  opens (spa A)}
  (r1 r2 : rational_open_data_subsets U) :
rational_open_data_subsets_inter r1 r2 = rational_open_data_subsets_inter r2 r1 :=
begin
  rw subtype.ext,
  exact rational_open_data.rational_open_data_symm r1.1 r2.1
end

instance (r : rational_open_data A) : uniform_space (rational_open_data.localization r) :=
topological_add_group.to_uniform_space _

instance (rd : rational_open_data A): uniform_add_group (rational_open_data.localization rd) :=
topological_add_group_is_uniform

def localization_map_is_uniform_continuous {r1 r2 : rational_open_data A} (h : r1 ‚â§ r2) :
  uniform_continuous (rational_open_data.localization_map h) :=
uniform_continuous_of_continuous (rational_open_data.localization_map_is_cts h)

end -- section

open uniform_space

-- r_o_d is short for "rational open data". KB needs to think more clearly
-- about namespaces etc.
/-- A<T/s>, the functions on D(T,s). A topological ring -/
def r_o_d_completion (r : rational_open_data A) :=
completion (rational_open_data.localization r)

namespace r_o_d_completion
open topological_space

noncomputable instance (r : rational_open_data A) : comm_ring (r_o_d_completion r) :=
by dunfold r_o_d_completion; apply_instance

instance uniform_space (r : rational_open_data A) : uniform_space (r_o_d_completion r) :=
by dunfold r_o_d_completion; apply_instance

instance (r : rational_open_data A) : topological_ring (r_o_d_completion r) :=
by dunfold r_o_d_completion; apply_instance

noncomputable def restriction {r1 r2 : rational_open_data A} (h : r1 ‚â§ r2) :
r_o_d_completion r1 ‚Üí r_o_d_completion r2 :=
completion.map (rational_open_data.localization_map h)

instance restriction_is_ring_hom {r1 r2 : rational_open_data A} (h : r1 ‚â§ r2) :
  is_ring_hom (restriction h) :=
completion.is_ring_hom_map (rational_open_data.localization_map_is_cts h)

lemma restriction_is_uniform_continuous {r1 r2 : rational_open_data A} (h : r1 ‚â§ r2) :
uniform_continuous (r_o_d_completion.restriction h) :=
completion.uniform_continuous_map

end r_o_d_completion -- namespace

open topological_space

/-- The underlying type of ùí™_X(U), the structure presheaf on Spa(A) -/
def presheaf_value (U : opens (spa A)) :=
{f : Œ† (rd : rational_open_data_subsets U), r_o_d_completion rd.1 //
   ‚àÄ (rd1 rd2 : rational_open_data_subsets U) (h : rd1.1 ‚â§ rd2.1),
     r_o_d_completion.restriction h (f rd1) = (f rd2)} -- agrees on overlaps

def presheaf_value_set (U : opens (spa A)) :=
{f : Œ† (rd : rational_open_data_subsets U), r_o_d_completion rd.1 |
   ‚àÄ (rd1 rd2 : rational_open_data_subsets U) (h : rd1.1 ‚â§ rd2.1),
     r_o_d_completion.restriction h (f rd1) = (f rd2)}

-- We need to check it's a ring


instance presheaf_subring (U : opens (spa A)) : is_subring (presheaf_value_set U) :=
begin
refine {..},
  { -- zero_mem
    intros rd‚ÇÅ rd‚ÇÇ h,
    exact is_ring_hom.map_zero _ },
  { -- add_mem
    intros a b ha hb rd‚ÇÅ rd‚ÇÇ h,
    change r_o_d_completion.restriction h (a rd‚ÇÅ + b rd‚ÇÅ) = a rd‚ÇÇ + b rd‚ÇÇ,
    rw is_ring_hom.map_add (r_o_d_completion.restriction h),
    rw [ha _ _ h, hb _ _ h] },
  { -- neg_mem
    intros a ha rd‚ÇÅ rd‚ÇÇ h,
    change r_o_d_completion.restriction h (-(a rd‚ÇÅ)) = -(a rd‚ÇÇ),
    rw is_ring_hom.map_neg (r_o_d_completion.restriction h),
    rw ha _ _ h },
  { -- one_mem
    intros rd‚ÇÅ rd‚ÇÇ h,
    exact is_ring_hom.map_one _ },
  { -- mul_mem
    intros a b ha hb rd‚ÇÅ rd‚ÇÇ h,
    change r_o_d_completion.restriction h (a rd‚ÇÅ * b rd‚ÇÅ) = a rd‚ÇÇ * b rd‚ÇÇ,
    rw is_ring_hom.map_mul (r_o_d_completion.restriction h),
    rw [ha _ _ h, hb _ _ h] }
end

noncomputable instance presheaf_comm_ring (U : opens (spa A)) : comm_ring (presheaf_value U) :=
begin
  apply @subset.comm_ring _ pi.comm_ring _ _, apply_instance,
  exact spa.presheaf_subring U
end

instance presheaf_top_space (U : opens (spa A)) : topological_space (presheaf_value U) :=
by unfold presheaf_value; apply_instance

example (U : opens (spa A)) :
  topological_ring (Œ† (rd : rational_open_data_subsets U), r_o_d_completion (rd.1)) :=
by apply_instance

-- tactic mode because I can't get Lean to behave. Note: switching to tactic
-- mode indicated the problem was that Lean was not finding the two instances I flag
-- with haveI and letI; probably now I know this one could try to go back into term mode.
instance presheaf_top_ring (U : opens (spa A)) : topological_ring (presheaf_value U) :=
begin
  haveI := spa.presheaf_subring U,
  letI : topological_ring (Œ† (rd : rational_open_data_subsets U), r_o_d_completion (rd.1)) :=
    by apply_instance,
  apply topological_subring (presheaf_value_set U),
end

instance (U : opens (spa A)) (r : rational_open_data_subsets U) :
  is_ring_hom (Œª (f : presheaf_value U), f.val r) :=
{ map_one := rfl,
  map_mul := Œª _ _, rfl,
  map_add := Œª _ _, rfl }

-- note the (X : _) trick, which tells Lean "don't try and
-- elaborate X assuming it has the type you know it has,
-- elaborate it independently, figure out the type, and
-- then unify". Thanks to Mario Carneiro for this trick which
-- hugely speeds up elaboration time of this definition.
def presheaf_map {U V : opens (spa A)} (hUV : U ‚â§ V) :
  presheaf_value V ‚Üí presheaf_value U :=
Œª f, ‚ü®_, Œª rd1 rd2 h,
  (f.2 (rational_open_data_subsets.map hUV rd1)
    (rational_open_data_subsets.map hUV rd2) h : _)‚ü©

lemma presheaf_map_id (U : opens (spa A)) :
  presheaf_map (le_refl U) = id :=
by { delta presheaf_map, tidy }

lemma presheaf_map_comp {U V W : opens (spa A)} (hUV : U ‚â§ V) (hVW : V ‚â§ W) :
  presheaf_map hUV ‚àò presheaf_map hVW = presheaf_map (le_trans hUV hVW) :=
by { delta presheaf_map, tidy }

instance presheaf_map_is_ring_hom {U V : opens (spa A)} (hUV : U ‚â§ V) :
is_ring_hom (presheaf_map hUV) :=
{ map_one := rfl,
  map_mul := Œª _ _, rfl,
  map_add := Œª _ _, rfl }

def presheaf_map_cts {U V : opens (spa A)} (hUV : U ‚â§ V) :
  continuous (presheaf_map hUV) :=
continuous_subtype_mk _ (continuous_pi (Œª i, ((continuous_apply _).comp continuous_subtype_val)))

variable (A)
noncomputable def presheaf_of_topological_rings : presheaf_of_topological_rings (spa A) :=
{ F := presheaf_value,
  res := Œª U V, presheaf_map,
  Hid := presheaf_map_id,
  Hcomp := Œª U V W, presheaf_map_comp,
  Fring := spa.presheaf_comm_ring,
  res_is_ring_hom := Œª U V, spa.presheaf_map_is_ring_hom,
  Ftop := spa.presheaf_top_space,
  Ftop_ring := spa.presheaf_top_ring,
  res_continuous := Œª U V, presheaf_map_cts
}


end spa -- namespace I think

-- old notes

-- remember that a rational open is not actually `rational_open s T` in full
-- generality -- we also need that T is finite and that T generates an open ideal in A.
-- The construction on p73/74 (note typo in first line of p74 -- ideal should be I.D)
-- gives A<T/s> (need completion) and A<T/s>^+ (need integral closure).

-- KB idle comment: I guess we never make A<T/s> a Huber pair if A is a Huber pair?
-- We would need integral closure for this and I don't think we have it in mathlib.

-- We see mid way through p75 that the definition of the presheaf
-- on V is proj lim of O_X(U) as U runs through rationals opens in V. This gets
-- the projective limit topology and then we have a presheaf (hopefully this is
-- straightforward) of complete topological rings (need proj lim of complete is complete)
